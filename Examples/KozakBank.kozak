Hetman

Importuvaty("hash");

Klas Rahunok{
    Zakhyshchenyy Chyslo balance;
    Zakhyshchenyy Ryadok type;
    Zakhyshchenyy DroboveChyslo interest_rate;

    DruhKlas: AdminRahunok;

    Tvir(account, balance, type){
        tsey.account := account;
        tsey.balance := balance;
        tsey.interest_rate := 0.0;
        tsey.type := type;
    }

    Zavdannya GetBalance(){
        Povernuty tsey.balance;
    }

    Zavdannya GetType(){
        Povernuty tsey.type;
    }

    Zavdannya GetAccount(){
        Povernuty tsey.account;
    }

    Zavdannya GetInterestRate(){
        Povernuty tsey.interest_rate;
    }

    Zavdannya Deposit(){
        deposit := DroboveChyslo(Slukhai("Enter the deposit: "));
        tsey.balance := tsey.balance + deposit;
        balance := tsey.balance;
        tsey.CheckBalance();
    }

    Zavdannya Withdraw(){
        withdraw := DroboveChyslo(Slukhai("Enter the amount you wish to withdraw (fees apply): "));
        Yakscho (tsey.balance<1000){
            fee := 5;
        }
        Inakshe{
            fee := 0.05 * withdraw;
        }
        tsey.balance := tsey.balance - withdraw - fee;
        balance := tsey.balance;
        Spivaty("Withdrawal successful. The fee of ", fee, " was applied");
        Yakscho(balance<0){
            Spivaty("Insufficient funds to withdraw!");
            tsey.balance := tsey.balance + withdraw;
            tsey.CheckBalance();
        }
        Inakshe{
        tsey.CheckBalance();
        tsey.balance := balance;
        }
    }
    
    Zavdannya CheckBalance(){
        Spivaty("Current funds for account " + tsey.account +" is " + tsey.balance);
    }

    Zavdannya Transfer(receiver, amount){
        Yakscho(tsey.balance >= amount){
            fee := 0.03*amount;
            tsey.balance := tsey.balance - amount-fee;
            receiver.balance := receiver.balance + amount;
                
            Spivaty("Transfer of " + amount + " successful!");
            Spivaty("The fee of ", fee, " was applied");
            Spivaty("Current funds for account " + tsey.account +" is " + tsey.balance);
            Spivaty("New funds for account " + receiver.account +" is " + receiver.balance);
        }
        Inakshe{
            Spivaty("Insufficient funds to transfer!");
            Spivaty("Current funds: " + tsey.balance);
        }
    }
}

Klas ZaoshchadnyyRahunok : Rahunok {

    Tvir(account, balance, interest_rate, type){
        // Initialize inherited properties
        tsey.account := account;
        tsey.balance := balance;
        tsey.interest_rate := interest_rate;
        tsey.type := type;
    }

    Zavdannya ApplyInterest(){
        interest := tsey.balance * tsey.interest_rate;
        tsey.balance := tsey.balance + interest;
        Spivaty("Interest applied! " + interest + " added to " + tsey.account);
        tsey.CheckBalance();
    }
    
    Zavdannya Withdraw(){
        withdraw := DroboveChyslo(Slukhai("Enter the amount you wish to withdraw (Savings, fee applies): "));
        fee := 5.00;
        amount_to_deduct := withdraw + fee;
        
        Yakscho(tsey.balance >= amount_to_deduct){
            tsey.balance := tsey.balance - amount_to_deduct;
            Spivaty("Withdrawal successful. A fee of 5.00 was applied.");
            tsey.CheckBalance();
        } 
        Inakshe {
            Spivaty("Insufficient funds to withdraw and cover the fee!");
            tsey.CheckBalance();
        }
    }
}


Klas AdminRahunok : Rahunok {
    
    Zakrytyy Ryadok password;
    // Constructor (Tvir) - Only needs a name, as its balance is not relevant
    Tvir(account, password){
        tsey.account := account;
        tsey.balance := 0.00;
        tsey.password := password;
        //Spivaty("Admin Account '" + tsey.account + "' created!");
    }

    Zavdannya AdminLogin(attempted_password){
        
        Yakscho(tsey.password == hash.sha256(attempted_password)){
            Spivaty("✅ SUCCESS: Admin login successful!");
            Povernuty Pravda; // Return true on success
        }
        Inakshe{
            Spivaty("❌ FAILURE: Invalid password. Access denied.");
            Povernuty Nepravda; // Return false on failure
        }
    }

    Zavdannya GetTotalBankBalance(account_list){
        total := 0.00;
        Spivaty("--- Admin Action: Calculating Total Bank Balance ---");
        
        Dlya account kozhen account_list {
            total := total + account.balance;
        }
        
        Spivaty("✅ SUCCESS: Total funds across " + dovzhyna(account_list) + " accounts is: " + total);
        Povernuty total;
    }

    // New Admin Method 2: Apply Interest to all Savings Accounts
    Zavdannya ApplyInterestToAll(account_list) {
        Spivaty("--- Admin Action: Applying Interest to All Eligible Accounts ---");
        applied_count := 0;

        Dlya account kozhen account_list {

            Yakscho(account.interest_rate > 0){ // Simple check: does the account have an interest_rate > 0?
                Spivaty("  -> Applying interest to: " + account.account);
                account.ApplyInterest(); // This calls the ApplyInterest method on the savings account object
                applied_count++;
            }
        }
        Spivaty("✅ SUCCESS: Interest applied to " + applied_count + " savings accounts.");
    }

    Zavdannya DodatyRahunok(all_accounts, new_account_name, new_account_object){
        Yakscho(mistyt(klyuchi(all_accounts), new_account_name)){
            Spivaty("❌ FAILURE: Account '" + new_account_name + "' already exists!");
            Povernuty Nepravda;
        }
        Inakshe{
            all_accounts[new_account_name] := new_account_object;
            Spivaty("✅ SUCCESS: Account '" + new_account_name + "' has been added to the bank.");
            Povernuty Pravda;
        }
    }

    Zavdannya VydalytyRahunok(accounts, account_name){
        Yakscho(mistyt(klyuchi(accounts), account_name)){
            vydalyty_klyuch(accounts, account_name); 
            Spivaty("✅ SUCCESS: Account '" + account_name + "' has been permanently removed.");
            Povernuty Pravda;
        }
        Inakshe{
            Spivaty("❌ FAILURE: Account '" + account_name + "' not found.");
            Povernuty Nepravda;
        }
    }

    Zavdannya CheckAccounts(all_accounts){
        Dlya account kozhen all_accounts {
        Spivaty("Account Name in Array: " + account.GetAccount());
        Spivaty("Account Balance: " + account.GetBalance());
        Spivaty("Account Type: " + account.GetType());
        Yakscho(account.GetType() == "Savings"){
            Spivaty("Interest rate for the account: ", account.interest_rate*100, "%");
        }
        }
    }
}



Admin := novyy AdminRahunok("Admin", "e7cf3ef4f17c3999a94f2c6f612e8a888e5b1026878e4e19398b23bd38ec221a"); //Password is "Password"

Oksana := novyy Rahunok("Oksana", 500.50, "Chequing");
Taras := novyy Rahunok("Taras", 1000, "Chequing");
Ivan := novyy ZaoshchadnyyRahunok("Ivan", 2500.0, 0.05, "Savings");
Mariya := novyy ZaoshchadnyyRahunok("Mariya", 500.0, 0.02, "Savings");

accounts := {
"Oksana" : Oksana,
"Taras" : Taras,
"Ivan" : Ivan,
"Mariya" : Mariya
};

all_accounts := [Oksana, Ivan, Taras, Mariya];


Account := Slukhai("Which account would you like to access: ");

Yakscho(mistyt(klyuchi(accounts), Account)){
    current := accounts[Account];
    Spivaty("Welcome " + current.GetAccount() + " (" + current.GetType() + ")");
    operation := Chyslo(Slukhai("What would you like to do? Deposit(1), Withdraw(2), CheckBalance(3), Transfer(4): "));
    Yakscho(operation == 1){
    current.Deposit();
    }
    Abo_Yakscho(operation == 2){
        current.Withdraw();
    }
    Abo_Yakscho(operation == 3){
        current.CheckBalance();
    }
    Abo_Yakscho(operation == 4){
        receiver := Slukhai("Enter the recipient account name: ");
        
        Yakscho(receiver == current.account){
            Spivaty("You cannot transfer money to yourself");
        }

        Abo_Yakscho(receiver != current.account){
        
            Yakscho(mistyt(klyuchi(accounts), receiver)){
                amount := DroboveChyslo(Slukhai("Enter the amount for transfer (fee of 3% apply): ")); 
                receiver := accounts[receiver];
                current.Transfer(receiver, amount);
            }
            Inakshe{
                Spivaty("Invalid input");
            }
        }
    }
}


Yakscho(Account == "Admin"){
Spivaty("--- TESTING SUCCESSFUL LOGIN ---");
attempt_pass := Slukhai("Enter Password for the account: ");

login_status := Admin.AdminLogin(attempt_pass);

Yakscho(login_status == Pravda){
    Spivaty("Login Status OK:", login_status);
    Access := Pravda;
}
Inakshe {
    Spivaty("Login Status FAIL:", login_status);
}

Yakscho (Access == Pravda){
    operation := Slukhai("What would you like to do? Get total Balance for the Bank(1), Apply interest to all accounts (2), Add account (3), Delete account(4), Check Accounts (5): ");
    Yakscho (operation == "1"){
        Admin.GetTotalBankBalance(all_accounts);
    }
    
    Abo_Yakscho (operation == "2"){
        Admin.ApplyInterestToAll(all_accounts);
    }
    
    Abo_Yakscho (operation == "3"){
        name := Slukhai("Enter the new account name: ");
        initial_deposit := DroboveChyslo(Slukhai("Enter the initial deposit for the account: "));
        type := Slukhai("What type of account? Chequing(1), Savings(2): ");
        interest_rate := 0;

        Yakscho(type == "1"){
            type := "Chequing";
            obj := novyy Rahunok(name, initial_deposit, type);
        }
        Abo_Yakscho(type == "2"){
            type := "Savings";
            Yakscho (initial_deposit<1000.0){
                interest_rate := 0.02;
            }
            Abo_Yakscho (initial_deposit>=1000.0){
                interest_rate := 0.05;
            }
            obj := novyy ZaoshchadnyyRahunok(name, initial_deposit, interest_rate, type);
        }

        Admin.DodatyRahunok(accounts, name, obj);

        dodaty(all_accounts, obj);

        Yakscho(mistyt(klyuchi(accounts), name)){
            Spivaty("✅ Verification: ", name, " is in the dictionary.");
        }
        Inakshe{
            Spivaty("❌ Verification: ", name, " is missing from the dictionary.");
        }
        // Check the object's balance
        Spivaty(name,"'s Balance:", accounts[name].GetBalance());

        Admin.CheckAccounts(all_accounts);
        
    }
    Abo_Yakscho(operation == "4"){
        account_to_delete := Slukhai("Enter the name of account to be deleted: ");
        index_account_delete := index_z(all_accounts, account_to_delete);
        delete_status := Admin.VydalytyRahunok(accounts, account_to_delete);
    
        Yakscho(delete_status == Pravda){            
            new_all_accounts := [];

            Dlya account kozhen all_accounts {
                
                Yakscho(account.account != account_to_delete){
                    dodaty(new_all_accounts, account); // Keep the object
                }
            }
            
            all_accounts := new_all_accounts;
            
        }
        Admin.CheckAccounts(all_accounts);
    }

    Abo_Yakscho(operation == "5"){
        Admin.CheckAccounts(all_accounts);
    }
}
}
